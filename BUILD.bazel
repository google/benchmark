licenses(["notice"])

load("//:config/generate_export_header.bzl", "generate_export_header")
load("//:config/generate_version_header.bzl", "generate_version_header")

# Generate header to provide ABI export symbols
generate_export_header(
    out = "include/benchmark/export.h",
    lib = "benchmark",
    static_define = "BENCHMARK_STATIC_DEFINE",
)

# Get the git version variables
py_binary(
    name = "get_git_version",
    srcs = ["config/get_git_version.py"],
    python_version = "PY3"
)

# Generate version header
generate_version_header(
    name = "generate_version_header",
    git_version_name = "GIT_VERSION",
    git_is_dirty_name = "GIT_IS_DIRTY",
    default_version = "DEFAULT_VERSION",
    header = "include/benchmark/version.h",
    src = "include/version.h.in"
)

config_setting(
    name = "qnx",
    constraint_values = ["@platforms//os:qnx"],
    values = {
        "cpu": "x64_qnx",
    },
    visibility = [":__subpackages__"],
)

config_setting(
    name = "windows",
    constraint_values = ["@platforms//os:windows"],
    values = {
        "cpu": "x64_windows",
    },
    visibility = [":__subpackages__"],
)

cc_library(
    name = "benchmark",
    srcs = glob(
        [
            "src/*.cc",
            "src/*.h",
        ],
        exclude = ["src/benchmark_main.cc"],
    ),
    hdrs = [
        "include/benchmark/benchmark.h",
        "include/benchmark/export.h", # From generate_export_header
        "include/benchmark/version.h", # From generate_version_header
    ],
    linkopts = select({
        ":windows": ["-DEFAULTLIB:shlwapi.lib"],
        "//conditions:default": ["-pthread"],
    }),
    strip_include_prefix = "include",
    visibility = ["//visibility:public"],
    # Only static linking is allowed; no .so will be produced.
    # Using `defines` (i.e. not `local_defines`) means that no
    # dependent rules need to bother about defining the macro.
    linkstatic = True,
    defines = ["BENCHMARK_STATIC_DEFINE"],
)

cc_library(
    name = "benchmark_main",
    srcs = ["src/benchmark_main.cc"],
    hdrs = ["include/benchmark/benchmark.h"],
    strip_include_prefix = "include",
    visibility = ["//visibility:public"],
    deps = [":benchmark"],
)

cc_library(
    name = "benchmark_internal_headers",
    hdrs = glob(["src/*.h"]),
    visibility = ["//test:__pkg__"],
)
